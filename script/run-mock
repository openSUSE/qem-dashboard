#!/usr/bin/env perl
use Mojo::Base -strict, -signatures;
use Mojo::File qw(curfile);
use lib curfile->dirname->sibling('lib')->to_string;
use lib curfile->dirname->sibling('t', 'lib')->to_string;
use Dashboard::Test;
use Dashboard;
use Mojolicious::Commands;

$ENV{MOJO_MODE} ||= 'development';
die 'TEST_ONLINE environment variable must be set (e.g. postgresql://postgres:postgres@localhost:5432/postgres)'
  unless $ENV{TEST_ONLINE};
my $dashboard_test = Dashboard::Test->new(
  online      => $ENV{TEST_ONLINE},
  schema      => 'js_ui_test',
  keep_schema => 1
);
$ENV{DASHBOARD_CONF_OVERRIDE} = Mojo::JSON::encode_json($dashboard_test->default_config);
my $app = Dashboard->new;
$dashboard_test->minimal_fixtures($app);
my $commands = Mojolicious::Commands->new(app => $app);
@ARGV = ('daemon', @ARGV) unless @ARGV;
if ($ENV{MOJO_MODE} eq 'development' && $ARGV[0] eq 'daemon' && !$ENV{DASHBOARD_NO_MORBO} && !$ENV{MORBO_INTERNAL}) {
  my $pid = fork();
  if ($pid == 0) {
    # Child: Start npm run watch
    # Use a new process group so we can terminate any sub-processes if needed
    setpgrp(0, 0);
    exec("npm run watch > /dev/null 2>&1") or die "Could not exec npm run watch: $!";
  }
  $ENV{MORBO_INTERNAL} = 1;
  my $cleanup = sub ($sig) {
    kill 'TERM', -$pid if $pid; # Terminate the whole process group
    exit;
  };
  $SIG{INT} = $SIG{TERM} = $cleanup;
  system 'morbo', $0, @ARGV;
  kill 'TERM', -$pid if $pid;
  exit;
}
$commands->run(@ARGV);
